
buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "com.wynprice.cursemaven:CurseMaven:2.1.4"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: "com.wynprice.cursemaven"

version = "1.0.7"
group = "xyz.phanta.tconevo"
archivesBaseName = "tconevo-1.12.2"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
    useDepAts = true
}

repositories {
    mavenLocal()
    maven {
        name = 'DVS1 Repo'
        url = 'http://dvs1.progwml6.com/files/maven/'
    }
    maven {
        name = "OpenComputers"
        url = "https://maven.cil.li"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven {
        name = "Thiakil Maven"
        url = "http://maven.thiakil.com/"
    }
    maven {
        name = "BlameJared Maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Bluexin Maven"
        url = "http://maven.bluexin.be/repository/snapshots/"
    }
}

dependencies {
    compile 'io.github.phantamanta44.libnine:libnine-1.12.2:1.1.8'
    deobfCompile 'mezz.jei:jei_1.12.2:4.15.0.293'
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.3.56"
    deobfCompile "slimeknights:TConstruct:1.12.2-2.13.0.184"
    deobfCompile "curse.maven:constructs-armory:2882794" // 1.2.5.4
    deobfCompile 'com.azanor.baubles:Baubles:1.12-1.5.2'
    deobfCompile "codechicken:CodeChickenLib:1.12.2-3.2.3.358:universal"
    deobfCompile "cofh:RedstoneFlux:1.12-2.1.0.7:universal"
    // get DE deps from curseforge because trying to get it from a proper repo Fs up dependencies extremely hard
    // because DE/PI depend on deobf jars for some reason and the deobf mappings versions differ
    deobfCompile "curse.maven:brandons-core:2867147" // 2.4.18.210
    deobfCompile "curse.maven:draconic-evolution:2867186" // 2.3.25.351
    deobfCompile "vazkii.botania:Botania:r1.10-363.148"
    deobfCompile "com.teamwizardry.librarianlib:librarianlib-1.12.2:4.19.1"
    deobfCompile "curse.maven:natural-pledge:2740703" // r3.1.2
    deobfCompile "cofh:CoFHCore:1.12.2-4.6.3.27:universal"
    deobfCompile "cofh:ThermalExpansion:1.12.2-5.5.4.43:universal"
    deobfCompile "curse.maven:industrial-foregoing:2745321"
    deobfCompile 'appeng:appliedenergistics2:rv6-stable-7'
    deobfCompile "curse.maven:mekanism:2835175" // 9.8.3.390
}

jar {
    manifest {
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLCorePlugin': 'xyz.phanta.tconevo.coremod.TconEvoCoreMod'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // don't copy asset generation scripts
    exclude '**/*.js'
    exclude '**/*.py'
    exclude '**/*.sh'
    
    // copy access transformers
    rename '.+_at\\.cfg', 'META-INF/$0'
}
